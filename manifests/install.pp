# @summary Install cvmfs from a yum repository.
# @api private
#
class cvmfs::install (
  String[1] $cvmfs_version = $cvmfs::cvmfs_version,
  Stdlib::Absolutepath $cvmfs_cache_base = $cvmfs::cvmfs_cache_base,
  Optional[Enum['yes','no']] $cvmfs_claim_ownership = $cvmfs::cvmfs_claim_ownership,
) inherits cvmfs {
  # Create the cache dir if one is defined, otherwise assume default is in the package.
  # Require the package so we know the user is in place.
  # We need to change the selinux context of this new directory below.
  $cache_seltype = 'cvmfs_cache_t'

  # Compare the default value with the one from hiera if declared
  $default_cvmfs_cache_base  = '/var/lib/cvmfs'

  if ($cvmfs_claim_ownership == 'yes') or ($cvmfs_cache_base != $default_cvmfs_cache_base) {
    file { $cvmfs_cache_base:
      ensure  => directory,
      owner   => $cvmfs::cvmfs_cache_owner,
      group   => $cvmfs::cvmfs_cache_group,
      mode    => $cvmfs::cvmfs_cache_mode,
      seltype => $cache_seltype,
      require => Package['cvmfs'],
    }
  }

  package { 'cvmfs':
    ensure => $cvmfs_version,
  }

  # Create a file for the cvmfs
  file { '/etc/cvmfs/cvmfsfacts.yaml':
    ensure  => file,
    mode    => '0644',
    content => "---\n#This file generated by puppet and is used by custom facts only.\ncvmfs_cache_base: ${cvmfs_cache_base}\n",
    require => Package['cvmfs'],
  }
}
